(def sibilize (input)
     (var result (|> input
                     parse
                     restructure
                     transpile
                     output-formatter))
     
     result)

(def sourcemap (input)
     (sourcemapper (transpile (restructure (parse input)))))

(set sibilant 'sibilize sibilize)

(def sibilant.version ()
     (get (sibilant.package-info) 'version))

(def sibilant.strip-shebang (data)
     (data.replace (regex "^#!.*\\n") "\n"))

(set sibilant 'file 'eval.sibilant)

(def with-dir-and-file (dir file fn)
     (var before { dir sibilant.dir
                   file sibilant.file })
     (set sibilant
          'dir  dir
          'file file)

     (var retval (fn))

     (set sibilant
          'dir  before.dir
          'file before.file)

     retval)

(set sibilant 'source-cache {})

(def sibilant.sibilize-file (file-name)
     (|> file-name
         sibilant.transpile-file
         output-formatter))

(def sibilant.sibilize-json (file-name)
     (var before sibilant.quote-hash-keys)
     (set sibilant 'quote-hash-keys true)
     (var content (sibilant.sibilize-file file-name))
     (set sibilant 'quote-hash-keys before)
     content)

