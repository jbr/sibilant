<!DOCTYPE html> 
<html> 
  <head> 
    <script src="/javascripts/jquery.js"></script> 
    <script src="/javascripts/jquery-ui.js"></script> 
    <script src="/javascripts/macros.lisp" type="text/lisp"></script> 
    <script src="/javascripts/sibilant.info.lisp" type="text/lisp"></script> 
    <script src="/javascripts/browser.js"></script> 
    <link rel="stylesheet" href="/stylesheets/sibilant.css"/>
    <title>Sibilant: Javascript with a lisp</title>
    <script type="text/javascript">
      var _gaq = _gaq || [];
      _gaq.push(['_setAccount', 'UA-17635250-1']);
      _gaq.push(['_trackPageview']);
      
      (function() {
        var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
        ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
        var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
      })();
    </script>
  </head>
  <body>
    <header>
      <h1>SibilantJS</h1>
      <h2>stuff</h2>
      <div id="controls"> 
	<a id="prev" href="#">Previous</a>
	<a id="next" href="#">Next</a>
      </div>
    </header>

    <a id="fork-me-on-github" href="http://github.com/jbr/sibilant"> 
      <span>Fork me on github</span> 
    </a> 
 
    <div id="left"> 
      <textarea name="content">
      </textarea> 
    </div> 
 
    <div id="right"> 
      <pre id="output">
      </pre>
    </div> 


    <script id="welcome" language="sibilant/example" data-title="Welcome">
; Welcome to the sibilant tutorial.
; Sibilant code and comments will
; appear in the left column and
; the equivalent javascript will
; be generated by your browser
; in the right column.
;
;
; You can edit the sibilant code
; at any time to see how the js
; changes.
;
;
; Press the next button
; to begin.
</script>

    <script id="numbers-and-strings"
	    language="sibilant/example"
	    data-title="Literals: Numbers & Strings">
<![CDATA[
; Numbers are directly
; translated to javascript.
;
5
10.5
-100.5
;
;
; Strings start and end
; with a double quote.
;
;
"hello world"
"he said to them \"hello\""
"multiline strings
are allowed"
]]>
    </script>

<script id="literal-variables"
	language="sibilant/example"
	data-title="Literals: Variable names">
<![CDATA[
"Variable names can only include lower-case
letters, the period (.) and the hyphen (-).
They may end with a question mark (?) or with a
bang (!). They may start with a sigil ($)."

simple

multi-word-variable-name

is-this-thing-on?

save!

variable-number-5

$jquery

]]>
</script>


<script id="quoting"
	language="sibilant/example"
	data-title="Quoting">
<![CDATA[
; Sibilant supports quoting
; with the single quote (').
; Anything that is a legal
; variable name can be
; quoted, as well as lists.

'a
'this-is-a-multi-word-quote
'(a b c d e)

; This is equivalent to:
;
(quote a)
(quote this-is-a-multi-word-quote)
(quote (a b c d e))
]]>
</script>


<script id="lists"
	language="sibilant/example"
	data-title="Lists">
<![CDATA[
; Lists are made with the list macro
; and can be nested infinitely.
; A list translates to a javascript
; array.

(list hello
      '(q r s)
      (list foo bar baz))
]]>
</script>

<script id="hashes"
	language="sibilant/example"
	data-title="Hashes/Objects">
<![CDATA[
; Sibilant supports javascript
; hash-objects by way of the
; hash macro:
(hash key-one value-one
      key-two value-two)

(hash "multi word keys are" 'quoted
      and '(any value is legal))

(hash a b c d e f)

(hash sub-hash (hash key value))]]>
</script>


<script id="defining-variables"
	language="sibilant/example"
	data-title="Defining Variables">
<![CDATA[
; Define a variable with the defvar
; macro. This translates directly
; to the var keyword in javascript.

(defvar foo 35)
(defvar wibble "test")
(defvar bar '(multi word array))

(defvar var1 val1
        var2 val2)

; To modify an existing variable,
; use setf.

(setf foo 10)
(setf a 1
      b 2)
]]>
</script>


<script id="functions"
	language="sibilant/example"
	data-title="Defining Functions">
<![CDATA[
(defun alert-hello (planet-name)
  (defvar message (concat "hello " planet-name))
  (alert message)
  message)

(defvar my-message (alert-hello 'world))
; my-message is now "hello world"
]]>
</script>

<script id="variable-count-arguments"
	language="sibilant/example"
	data-title="Variable count arguments">
<![CDATA[
(defun alert-each-argument(&rest my-arguments)
  (each (arg) my-arguments (alert arg)))

(alert-each-argument 'hello 'world 'foo)
]]>
</script>

<script id="optional-arguments"
	language="sibilant/example"
	data-title="Optional arguments">
<![CDATA[
; Optional arguments are specified with
; &optional. If there are fewer than the
; total number of possible arguments,
; optional arguments are skipped.  If
; there are several, they are filled
; from left to right.

(defun has-optional-arg (&optional optional-arg
                         mandatory-arg)
  (when (defined? optional-argument)
    (alert "optional provided"))
  mandatory-arg)

(has-optional-arg "only one arg")
(has-optional-arg true "return value")
]]>
</script>


<script id="lambdas"
	language="sibilant/example"
	data-title="Lambdas">
<![CDATA[
; (lambda) works much like (defun)
; This example uses Node's async
; readfile

(defvar fs (require 'fs))

(fs.read-file "path/to/file"
  (lambda (error content)
    (alert (or error content))))
]]>
</script>


<script id="when"
	data-title="When"
	language="sibilant/example">
<![CDATA[
; `when` is the simplest conditional.
; It only considers the true case.
; `when` will return the value of the
; last statement

(defvar this-will-be-true (when (= 5 (+ 3 2))
  (alert "we've got a five")
  (post-to-twitter "three and two is five")
  true))
]]>
</script>

<script id="if" data-title="If"
	language="sibilant/example">
<![CDATA[
; `if` is like `when` with an "else."
(defvar a-or-b (if (< 0.5 (-math.random)) 'a 'b))

; Unlike when, cases may only be a single
; statement. Use progn to execute several
; statements.

(defvar a-or-b
  (if (< 0.5 (-math.random))
    (progn
      (log "random was more than 0.5")
      'a)
    (progn
      (log "random was less than 0.5")
      'b)))
]]>
</script>

<script id="switch"
	data-title="Switch"
	language="sibilant/example">
<![CDATA[
; Switch allows you to check a variable
; against any number of possible values.

(defvar comment (switch month-name
  ('january
    (increment year)
    "happy new year!")
  ('(february march) "still pretty cold")
  ('april "expect showers")
  ("may" "flowers!")
  ('(june july august)
    (enjoy 'sunshine)
    'summer)
  ('(september october) "leaves change")
  ('(november december) "getting chilly")
  (default
    (record-unexpected-month month-name)
    "I didn't recognize that")))

]]>
</script>

<script id="iteration-with-each"
	data-title="Iteration with each"
	language="sibilant/example">
<![CDATA[
; To iterate over an array, use the each
; macro.

(defvar list '(a b c d e))
(each (letter) list
  (when (= "C" (letter.to-upper-case))
    (console.log "this was a c"))
  (console.log letter))

]]>
</script>

<script id="while"
	data-title="While"
	language="sibilant/example">
<![CDATA[
(defvar wall-bottle-count 99)

(defun take-one-down ()
  (decr wall-bottle-count)
  the-bottle)

(while (> wall-bottle-count 0)
  (defvar bottle (take-one-down))
  (each (person) room
    (while (not (empty? bottle))
      (sip bottle))))
  
]]>
</script>

<script id="chain"
	data-title="Chaining function calls"
	language="sibilant/example">
<![CDATA[
; It is often idiomatic in javascript libraries
; to chain function calls. Here's how that looks
; in sibilant:

(chain ($ ".some.random#tag")
       (show)
       (css (hash background-color 'red))
       (animate (hash width 100))
       (click (lambda (evt)
         (chain ($ this)
           (hide)
           (data 'clicked true))
         true)))
]]>
</script>

<script id="macro-intro"
	data-title="Sibilant is mostly macros"
	language="sibilant/example">
<![CDATA[
"Now we're getting into advanced stuff.
Go get a cup of tea or some fresh air.
I'll be here when you get back.

The real power of sibilant is the ability
to define (and redefine) macros, extending
the language.  All constructs can be
redefined. This includes core language
features like +, lambda, if, defun, defvar,
and even defmacro itself.

Defining macros is as easy as defining
functions, but modify the interpreter
instead of outputting compiled js.
Most of sibilant is defined like this [1].

[1] http://j.mp/sibilant-macros"

]]>
</script>

<script id="defining-macros"
	data-title="Defining your own macros"
	language="sibilant/example">
<![CDATA[
; As an example, let's say you want to
; add a trinary macro to sibilant.

(defmacro trinary (cond true-value false-value)
  (concat (translate cond) " ? "
          (translate true-value) " : "
          (translate false-value) ";"))

(trinary (< 0.5 (-math.random)) 'yep 'nah)

; once removed, trinary is just a function call
(delmacro trinary)
(trinary (< 0.5 (-math.random)) 'yep 'nah)
]]>
</script>


<script id="redefining-macros"
	data-title="Redefining existing macros"
	language="sibilant/example">
<![CDATA[
; All macros can be redefined, and
; most of sibilant is macros.

; Let's say we want to add newlines
; after the commas in a list if
; there are more than two items.

(defmacro list (&rest args)
  (concat "["
    (if (> 3 args.length)
      (concat " "
        (join ", " (map args translate)) " ")
      (indent (join ",\n" (map args translate))))
    "]"))

(list a b)
(list a b c)
]]>
</script>

<script id="macroexpand"
	data-title="Expanding macros"
	language="sibilant/example">
<![CDATA[
; Want to find out how a macro is
; defined in js? Try macroexpand.

(macroexpand if)

(macroexpand defined?)

(macroexpand first)

]]>
</script>


<script id="macro-list"
	data-title="Listing available macros"
	language="sibilant/example">
<![CDATA[
; use macro-list to find out what
; macros are available

(macro-list)
]]>
</script>



  </body>
</html>
